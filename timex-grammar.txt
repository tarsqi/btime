timex -> time | date | interval

time -> named_hour { Time(_[0]) }
    | hour ":" minute { Time(_[0], _[2]) }
    | minute r"past|after" hour { Time(_[2], _[0]) }
    | minute r"of|to|before|'?till" hour { Time(_[2]-1, 60-int(_[0])) }

hour -> named_hour
    | small { Hour(_[0]) }

named_hour -> small r"o'?clock" { Hour(_[0]) }
    | "noon" { Hour(12) }
    | "midnight" { Hour(0) }

minute -> small { Minute(_[0]) }
    | "quarter" { Minute(15) }
    | "half" { Minute(30) }

date -> day month year { CalendarDate(_[2], _[1], _[0]) }
    | month day year { CalendarDate(_[2], _[0], _[1]) }

day -> small { DayOfMonth(_[0]) }
    | DayOfMonthToken() { DayOfMonth(_[0]) }

month -> MonthNumberToken() { Month(_[0]) }
    | Abbrev("january", 3) { Month(1) }
    | Abbrev("february", 3) { Month(2) }
    | Abbrev("march", 3) { Month(3) }
    | Abbrev("april", 3) { Month(4) }
    | "may" { Month(5) }
    | Abbrev("june", 3) { Month(6) }
    | Abbrev("july", 3) { Month(7) }
    | Abbrev("august", 3) { Month(8) }
    | Abbrev("september", 3) { Month(9) }
    | Abbrev("october", 3) { Month(10) }
    | Abbrev("november", 3) { Month(11) }
    | Abbrev("december", 3) { Month(12) }

year -> cardinal | r"[0-9]{1,4}$" { Year(_[0]) }

interval -> duration

duration -> period { Duration() | _[0] }
    | duration period { _[0] | _[1] }
    | duration "and" period { _[0] | _[2] }
    | duration "period"

period -> number r"seconds?" { Seconds(_[0]) }
    | number r"minutes?" { Minutes(_[0]) }
    | number r"hours?" { Hours(_[0]) }
    | number r"days?" { Days(_[0]) }
    | number r"weeks?" { Weeks(_[0]) }
    | number r"months?" { Months(_[0]) }
    | number r"years?" { Years(_[0]) }

number -> r"[0-9]+$" { int(_[0]) }
    | cardinal

cardinal -> zero | small | hundreds | thousands
zero -> "0" { 0 }
    | r"(zero|oh?|n?[ao]ught)$" { 0 }
small -> r"[0-9]{1,2}$" { int(_[0]) }
    | ones
    | zero ones { _[1] }
    | teen
    | tens
    | tens ones { _[0] + _[1] }
ones -> "one" { 1 }
    | "two" { 2 }
    | "three" { 3 }
    | "four" { 4 }
    | "five" { 5 }
    | "six" { 6 }
    | "seven" { 7 }
    | "eight" { 8 }
    | "nine" { 9 }
teen -> "ten" { 10 }
    | "eleven" { 11 }
    | "twelve" { 12 }
    | "thirteen" { 13 }
    | "fourteen" { 14 }
    | "fifteen" { 15 }
    | "sixteen" { 16 }
    | "seventeen" { 17 }
    | "eighteen" { 18 }
    | "nineteen" { 19 }
tens -> "twenty" { 20 }
    | "thirty" { 30 }
    | "forty" { 40 }
    | "fifty" { 50 }
    | "sixty" { 60 }
    | "seventy" { 70 }
    | "eighty" { 80 }
    | "ninety" { 90 }
hundreds -> small "hundred" { _[0] * 100 }
    | small "hundred" small { (_[0] * 100) + _[2] }
    | small "hundred" "and" small { (_[0] * 100) + _[3] }
thousands -> small small { _[0] * 100 + _[1] }
    | small "thousand" { _[0] * 1000 }
    | small "thousand" small { (_[0] * 1000) + _[2] }
    | small "thousand" "and" small { (_[0] * 1000) + _[3] }
    | small "thousand" hundreds { (_[0] * 1000) + _[2] }
    | hundreds "thousand" { _[0] * 1000 }
    | hundreds "thousand" small { (_[0] * 1000) + _[2] }
    | hundreds "thousand" "and" small { (_[0] * 1000) + _[3] }
    | hundreds "thousand" hundreds { (_[0] * 1000) + _[2] }
